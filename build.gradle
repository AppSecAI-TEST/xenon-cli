plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.4'
    // ide
    id 'idea'
    id 'eclipse'
    // test coverage
    id 'jacoco'
    id 'org.sonarqube' version '2.3'
    // docker build & push
    id 'maven-publish'
    id 'com.palantir.docker' version '0.11.0'
}

description = 'Perform files and jobs operations with Xenon library from command line'
version = '1.0.1'
group = 'nlesc'
mainClassName = 'nl.esciencecenter.xenon.cli.Main'
applicationName = 'xenon'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'https://dl.bintray.com/palantir/releases' // docker-compose-rule is published on bintray, not jcenter
    }
}

dependencies {
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'net.sourceforge.argparse4j:argparse4j:0.7.0'
    compile 'nl.esciencecenter.xenon:xenon:1.2.1'
    compile 'org.slf4j:slf4j-api:1.7.21'

    testCompile 'junit:junit:4.12'
    testCompile 'com.github.geowarin:docker-junit-rule:1.1.0'
    testCompile 'com.github.stefanbirkner:system-rules:1.16.0'
    testCompile 'com.palantir.docker.compose:docker-compose-rule-junit4:0.31.1'
}

docker {
    name 'nlesc/xenon-cli'
    dependsOn tasks.installShadowApp
    files tasks.installShadowApp.outputs
}
dockerfileZip.enabled = false

// use shadow dists everywhere
distZip.enabled = false
distTar.enabled = false
assemble.dependsOn distShadowTar
assemble.dependsOn distShadowZip

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}

sonarqube {
    properties {
        property 'sonar.host.url', 'https://sonarqube.com'
        property 'sonar.organization', project.group
    }
}